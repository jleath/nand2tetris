// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    // Internal Pins
    Not(in=instruction[15], out=aInstr);
    Not(in=aInstr, out=cInstr);
    Or(a=instruction[5], b=aInstr, out=loadA);
    And(a=instruction[4], b=cInstr, out=dLoad);
    And(a=cInstr, b=instruction[3], out=writeM);

    // ALU Logic
    Mux16(a=aRegisterOut, b=inM, sel=instruction[12], out=mOrAOut);
    ALU(x=dRegisterOut, y=mOrAOut, zx=instruction[11], nx=instruction[10],
        zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6],
        out=outM, out=aluOut, zr=zrOut, ng=ngOut);

    // D Register Logic
    DRegister(in=aluOut, load=dLoad, out=dRegisterOut);

    // A Register Logic
    Mux16(a=aluOut, b=instruction, sel=aInstr, out=insOrAOut);
    ARegister(in=insOrAOut, load=loadA, out=aRegisterOut, out[0..14]=addressM);

    // PC Logic
    
    Not(in=zrOut, out=notZr);
    Not(in=ngOut, out=notNg);
    And(a=notZr, b=notNg, out=notZrAndNotNg);
    Or(a=zrOut, b=notNg, out=zrOrNotNg);
    Or(a=zrOut, b=ngOut, out=zrOrNg);
    DMux8Way(in=true, sel=instruction[0..2], a=jumpA, b=jumpB, c=jumpC, d=jumpD, e=jumpE,
             f=jumpF, g=jumpG, h=jumpH);
    And(a=jumpA, b=false, out=jumpAYes);
    And(a=jumpB, b=notZrAndNotNg, out=jumpBYes);
    And(a=jumpC, b=zrOut, out=jumpCYes);
    And(a=jumpD, b=zrOrNotNg, out=jumpDYes);
    And(a=jumpE, b=ngOut, out=jumpEYes);
    And(a=jumpF, b=notZr, out=jumpFYes);
    And(a=jumpG, b=zrOrNg, out=jumpGYes);
    And(a=jumpH, b=true, out=jumpHYes);
    Or8Way(in[0]=jumpAYes, in[1]=jumpBYes, in[2]=jumpCYes, in[3]=jumpDYes,
           in[4]=jumpEYes, in[5]=jumpFYes, in[6]=jumpGYes, in[7]=jumpHYes, out=loadFlag);
    And(a=loadFlag, b=cInstr, out=loadBit);
    Not(in=loadBit, out=incFlag);
    PC(in=aRegisterOut, load=loadBit, inc=incFlag, reset=reset, out[0..14]=pc);
}
